DATA_DIR = config["output_base_dir"].rstrip("/")

rule diffbind:
    input:
        consensus_peaks = f"{DATA_DIR}/counts/{{mark}}_consensus.bed",
        metadata = "config/diffbind_config.csv",
        genes = config["GENES"]
    output:
        rds_file = f"{DATA_DIR}/diffbind/{{mark}}/{{mark}}_DBAobj.rds"
    params:
        padj_cutoff = config["padj_cutoff"],
        outdir = f"{DATA_DIR}/diffbind/{{mark}}"
    conda:
        "../envs/diffbind.yml"
    singularity:
        os.path.join(config["SINGULARITY_IMAGE_FOLDER"], "diffbind.sif")
    shell:
        "Rscript src/diffbind.R -m {input.metadata} -c {input.consensus_peaks} -g {input.genes} -p {params.padj_cutoff} -o {params.outdir}"
# normalize by entire sequencing depth

# note: additional plots are generated by script but not specified in rule output
rule diffbind_plots:
    input:
        rds_file = f"{DATA_DIR}/diffbind/{{mark}}/{{mark}}_DBAobj.rds"
    output:
        # outdir = directory("data/diffbind/{mark}")
        pca_plot = f"{DATA_DIR}/diffbind/{{mark}}/{{mark}}_pca.pdf"
    params:
        padj_cutoff = config["padj_cutoff"],
        outdir = f"{DATA_DIR}/diffbind/{{mark}}"
    conda:
        "../envs/diffbind.yml"
    singularity:
        os.path.join(config["SINGULARITY_IMAGE_FOLDER"], "diffbind.sif")
    shell:
        "Rscript src/diffbind_plots.R -i {input.rds_file} -p {params.padj_cutoff} -o {params.outdir}"


rule deseq2:
    input:
        counts=f"{DATA_DIR}/counts/{{mark}}_counts.tsv",
        meta="config/deseq2_metadata.csv",
        genes=config["GENES"]
    output:
        pcaPlot=f"{DATA_DIR}/deseq2/{{mark}}/{{mark}}-rld-pca.pdf",
        pcaPlotVsd=f"{DATA_DIR}/deseq2/{{mark}}/{{mark}}-vsd-pca.pdf",
        normCounts=f"{DATA_DIR}/deseq2/{{mark}}/{{mark}}-normcounts.csv",
        lnormCounts=f"{DATA_DIR}/deseq2/{{mark}}/{{mark}}-lognormcounts.csv",
        sdMeanRld=f"{DATA_DIR}/deseq2/{{mark}}/{{mark}}-rld.pdf",
        sdMeanVsd=f"{DATA_DIR}/deseq2/{{mark}}/{{mark}}-vsd.pdf",
        sampleDistVsd=f"{DATA_DIR}/deseq2/{{mark}}/{{mark}}-vsd-dist.pdf",
        sampleDistRld=f"{DATA_DIR}/deseq2/{{mark}}/{{mark}}-rld-dist.pdf",
        rds=f"{DATA_DIR}/deseq2/{{mark}}/{{mark}}-dds.rds"
    params:
        mark=lambda wildcards: wildcards.mark,
        outdir = f"{DATA_DIR}/deseq2/",
        numclusters = 4
    conda:
        "../envs/deseq2.yml"
    singularity:
        os.path.join(config["SINGULARITY_IMAGE_FOLDER"], "deseq2.sif")
    script:
        "src/deseq2.R"
# normalize by reads in peaks
